apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'
apply from: rootProject.file("quality.gradle")

android {
    compileSdkVersion rootProject.ext.compileSdk
    defaultConfig {
        applicationId "com.nicholasdoglio.eyebleach"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 1
        versionName "0.0.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    //Android
    implementation "com.android.support:appcompat-v7:$rootProject.support"
    implementation "com.android.support:support-v4:$rootProject.support"
    implementation "com.android.support:recyclerview-v7:$rootProject.support"
    implementation "com.android.support.constraint:constraint-layout:$rootProject.constraint"

    //Anko
    implementation "org.jetbrains.anko:anko-appcompat-v7-commons:$rootProject.anko"

    //ktx
    implementation "androidx.core:core-ktx:$rootProject.ktx"

    //Glide
    implementation "com.github.bumptech.glide:glide:$rootProject.glide"
    kapt "com.github.bumptech.glide:compiler:$rootProject.glide"
    implementation("com.github.bumptech.glide:recyclerview-integration:$rootProject.glide") {
        transitive = false
    }

    //Retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:$rootProject.retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit"

    //Moshi
    implementation "com.squareup.moshi:moshi:$rootProject.moshi"

    //Rx
    implementation "io.reactivex.rxjava2:rxjava:$rootProject.rxJava"
    implementation "io.reactivex.rxjava2:rxandroid:$rootProject.rxAndroid"
    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rootProject.rxbinding"
    implementation "com.jakewharton.rxrelay2:rxrelay:$rootProject.rxRelay"

    //AutoDispose
    implementation "com.uber.autodispose:autodispose-kotlin:$rootProject.dispose"
    implementation "com.uber.autodispose:autodispose-android-kotlin:$rootProject.dispose"
    implementation "com.uber.autodispose:autodispose-android-archcomponents-kotlin:$rootProject.dispose"

    //Dagger
    implementation "com.google.dagger:dagger:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android:$rootProject.dagger"
    implementation "com.google.dagger:dagger-android-support:$rootProject.dagger"
    kapt "com.google.dagger:dagger-compiler:$rootProject.dagger"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.dagger"

    //Android Architecture Components
    implementation "android.arch.paging:runtime:$rootProject.paging"
    implementation "android.arch.lifecycle:common-java8:$rootProject.archComponents"
    implementation "android.arch.lifecycle:extensions:$rootProject.archComponents"
    implementation "android.arch.lifecycle:reactivestreams:$rootProject.archComponents"
    implementation "android.arch.persistence.room:runtime:$rootProject.room"
    implementation "android.arch.persistence.room:rxjava2:$rootProject.room"
    kapt "android.arch.persistence.room:compiler:$rootProject.room"

    //Debugging
    implementation "com.jakewharton.timber:timber:$rootProject.timber"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.canary"
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.canary"
    implementation("com.crashlytics.sdk.android:crashlytics:$rootProject.crashlytics") {
        transitive = true
    }

    //Testing
    testImplementation "com.squareup.retrofit2:retrofit-mock:$rootProject.retrofit"
    testImplementation "com.squareup.okhttp3:mockwebserver:$rootProject.okhttp"
    testImplementation "junit:junit:$rootProject.junit"
    testImplementation "org.mockito:mockito-core:$rootProject.mockito"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$rootProject.mokitoKotlin"
    testImplementation "org.robolectric:robolectric:$rootProject.robo"
    testImplementation "android.arch.core:core-testing:$rootProject.archComponents"
    testImplementation "android.arch.persistence.room:testing:$rootProject.room"
    androidTestImplementation "com.android.support.test:rules:$rootProject.espressoTest"
    androidTestImplementation "com.android.support.test:runner:$rootProject.espressoTest"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$rootProject.espresso"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$rootProject.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

// Ensure the no-op dependency is always used in JVM tests.
configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}